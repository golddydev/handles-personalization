spending handles_personalization

enum Redeemer {
    PERSONALIZE { 
        handle: String
        designer: Map[String]Data
    }
    MIGRATE { 
        handle: String
    }
    RESET_IMAGE { 
        handle: String
        designer: Map[String]Data
    }
}

struct PzSettings {
    treasury_fee: Int
    treasury_cred: ByteArray
    pz_min_fee: Int
    pz_providers: Map[ByteArray]ByteArray
    valid_contracts: []ByteArray
    admin_creds: []ByteArray
    settings_cred: ByteArray
}

struct Datum {
    nft: Map[String]Data
    version: Int
    extra: Map[String]Data
}

const HANDLE_POLICY: MintingPolicyHash = MintingPolicyHash::new(#f0ff48bbb7bbe9d59a40f1ce90e9e9d0ff5002ec48f232b49ca0fb9a)
const LBL_444: ByteArray = #001bc280 // (444)bigirishlion
const LBL_222: ByteArray = #000de140 // (222)bigirishlion
const LBL_100: ByteArray = #000643b0 // (100)bigirishlion
const PZ_HANDLE: AssetClass = AssetClass::new(HANDLE_POLICY, LBL_222 + ("pz_settings".encode_utf8()))

func policyAndAssetSearch(output: TxOutput, policy: MintingPolicyHash, name: ByteArray, amount: Int, starts_with: Bool) -> Option[AssetClass] {
    output.value.to_map().fold_lazy((key: MintingPolicyHash, value: Map[ByteArray]Int, next: () -> Option[AssetClass]) -> {
        asset_name: Option[ByteArray] = value.find_key_safe((k: ByteArray) -> Bool { if (starts_with) { k.starts_with(name) } else { k == name } });
        if (policy == key && asset_name != Option[ByteArray]::None && value.get(asset_name.unwrap()) >= amount) {
            Option[AssetClass]::Some{AssetClass::new(key, asset_name.unwrap())}
        } else { next() }
    }, Option[AssetClass]::None)
}

func findAsReferenceOrOutput(policy: MintingPolicyHash, name: ByteArray, amount: Int, ctx: ScriptContext, starts_with: Bool, check_sig: Bool) -> (Option[TxOutput], Option[AssetClass]) {
    (tx: Option[TxOutput], asset: Option[AssetClass]) = ctx.tx.ref_inputs.fold_lazy(
        (input: TxInput, next: () -> () -> (Option[TxOutput], Option[AssetClass])) -> {
            a: Option[AssetClass] = policyAndAssetSearch(input.output, policy, name, amount, starts_with);
            b: Bool = if (check_sig) {
            ctx.tx.signatories.any((signer: PubKeyHash) -> {
                input.output.address.credential == Credential::new_pubkey(signer)
            })
            } else {true};

            if (a != Option[AssetClass]::None && b) {
                () -> {(Option[TxOutput]::Some{input.output}, a)}
            } else { 
            next() 
            }
        }, 
        () -> {(Option[TxOutput]::None, Option[AssetClass]::None)}
    )();
    tx.switch {
        Some => (tx, asset), 
        else => {
            ctx.tx.outputs.fold_lazy(
                (output: TxOutput, next: () -> () -> (Option[TxOutput], Option[AssetClass])) -> {
                    a: Option[AssetClass] = policyAndAssetSearch(output, policy, name, amount, starts_with);
                    if (a != Option[AssetClass]::None) {
                        () -> {(Option[TxOutput]::Some{output}, a)}
                    } else { next() }
            }, () -> {(Option[TxOutput]::None, Option[AssetClass]::None)})()
        }
    }
}

func getHandleTokens(handle: String, ctx: ScriptContext, check_sig: Bool) -> (TxOutput, TxOutput) {
    (reference_token: Option[TxOutput], _) = findAsReferenceOrOutput(HANDLE_POLICY, LBL_100 + (handle.encode_utf8()), 1, ctx, false, false);
    (owner_token: Option[TxOutput], _) = findAsReferenceOrOutput(HANDLE_POLICY, LBL_222 + (handle.encode_utf8()), 1, ctx, false, check_sig);
    assert(owner_token != Option[TxOutput]::None, "Handle input not present");
    assert(reference_token != Option[TxOutput]::None, "Reference Token input not present");
    assert(reference_token.unwrap().address.credential == Credential::new_validator(ctx.get_current_validator_hash()), "Reference Token not returned to contract");
    (owner_token.unwrap(), reference_token.unwrap())
}

func loadPzSettings(ctx: ScriptContext) -> PzSettings {
    pz_input: Option[TxInput] = ctx.tx.ref_inputs.find_safe((input: TxInput) -> Bool { 
        input.output.value.get_safe(PZ_HANDLE) == 1
    });
    assert(pz_input != Option[TxInput]::None, "pz_settings reference input not present");
    settings = PzSettings::from_data(pz_input.unwrap().output.datum.get_inline_data());
    assert(pz_input.unwrap().output.address.credential == Credential::new_validator(ValidatorHash::new(settings.settings_cred)), "pz_settings reference input not from ADA Handle");

    settings
}

func getApproverDatum(handle:String, settings: PzSettings, ctx: ScriptContext) -> Map[ByteArray]Map[ByteArray][]Int {
    input: Option[TxInput] = ctx.tx.ref_inputs.find_safe((input: TxInput) -> Bool { 
        input.output.value.get_safe(AssetClass::new(HANDLE_POLICY, LBL_222 + (handle.encode_utf8()))) == 1 
        || (input.output.value.contains_policy(HANDLE_POLICY) && input.output.value.get_policy(HANDLE_POLICY).any((asset_name: ByteArray, count: Int) -> Bool {
                asset_name.ends_with(("@" + handle).encode_utf8()) && count == 1
            }))
        && settings.pz_providers.any((_, cred: ByteArray) -> Bool { input.output.address.credential == Credential::new_validator(ValidatorHash::new(cred)) })
    });
    assert(input != Option[TxInput]::None, handle + " reference input not present or not from valid provider");
    Map[ByteArray]Map[ByteArray][]Int::from_data(input.unwrap().output.datum.get_inline_data())
}

func checkInputsAndLoadSettings(ctx: ScriptContext) -> (PzSettings, Map[String]Map[ByteArray]Map[ByteArray][]Int) {
    // TODO: Add @acc_policy_ids (for accessories directly on the Handle - image borders [pfp/bg], santa/leprechaun hats, halloween bats, etc)
    settings: PzSettings = loadPzSettings(ctx);
    bg_approver_datum: Map[ByteArray]Map[ByteArray][]Int = getApproverDatum("bg_policy_ids", settings, ctx);
    pfp_approver_datum: Map[ByteArray]Map[ByteArray][]Int = getApproverDatum("pfp_policy_ids", settings, ctx);

    (settings, Map[String]Map[ByteArray]Map[ByteArray][]Int{"bg_approvers": bg_approver_datum, "pfp_approvers": pfp_approver_datum})
}

func feesArePaid(ctx: ScriptContext, settings: PzSettings, handle: String) -> Bool {
    // Treasury fee paid
    assert(ctx.tx.outputs.any((output: TxOutput) -> Bool {
        // Check that datum is attached so it can be spent (also guard against double satisfaction)
        output.address.credential == Credential::new_validator(ValidatorHash::new(settings.treasury_cred))
            && output.value.contains(Value::lovelace(settings.treasury_fee))
            && output.datum.get_inline_data().switch{b: ByteArray => { b == handle.encode_utf8() }, else => {false}}
    }), "Handle treasury fee unpaid");

    // PZ provider fee paid
    assert(ctx.tx.outputs.any((output: TxOutput) -> Bool {
        // Check that datum is attached so it can be spent (also guard against double satisfaction)
        settings.pz_providers.any((_, cred: ByteArray) -> Bool { output.address.credential == Credential::new_validator(ValidatorHash::new(cred)) })
        && output.value.contains(Value::lovelace(settings.pz_min_fee))
        && output.datum.get_inline_data().switch{b: ByteArray => { b == handle.encode_utf8() }, else => {false}}
    }), "Personalization provider not found or fee unpaid");

    true
}

func isValidContract(hash: ValidatorHash, settings: PzSettings) -> Bool {
    assert(settings.valid_contracts.any((cred: ByteArray) -> Bool {
        Credential::new_validator(hash) == Credential::new_validator(ValidatorHash::new(cred))
    }), "Contract not found in valid contracts list");
    
    true
}

func cborHashesToCid(designer: Map[String]Data, reference_token: TxOutput) -> Bool {
    //print("REDEEMER: " + designer.serialize().show());
    datum = Datum::from_data(reference_token.datum.get_inline_data());
    ipfs = ByteArray::from_data(datum.extra.get("designer"));
    cid: String = ipfs.slice(8, ipfs.length).decode_utf8(); // This strips away "ipfs://z" (the z can be removed - see CID multiformats)
    multihash: ByteArray = Int::from_base58(cid).to_big_endian();
    content_hash: ByteArray = multihash.slice(4, multihash.length); // This strips away 0x01 0x70 0x12 0x20 (CIDv1, base58, sha2, 32 bytes)
    // print("cid = " + cid);
    // print("multihash = " + multihash.show());
    // print("designer.show() = " + designer.show());
    // print(content_hash.show() + " = " + designer.serialize().sha2().show());
    assert(content_hash == designer.serialize().sha2(), "Personalization designer settings hash doesn't match CID multihash");

    true
}

func parseAsset(asset_bytes: ByteArray) -> AssetClass {
    assert(asset_bytes.length > 28, "Asset is defined but not valid");
    policy_id: ByteArray = asset_bytes.slice(0, 28);
    policy = MintingPolicyHash::new(policy_id);
    name: ByteArray = asset_bytes.slice(28, asset_bytes.length);
    AssetClass::new(policy, name)
}

func getAssetDatum(asset: AssetClass, ctx: ScriptContext) -> OutputDatum {
    output: Option[TxOutput] = (
        if (asset.token_name.starts_with(LBL_222) || asset.token_name.starts_with(LBL_444)) {
            real_name: ByteArray = asset.token_name.slice(4, asset.token_name.length);
            (tx: Option[TxOutput], _) = findAsReferenceOrOutput(asset.mph, LBL_100 + real_name, 1, ctx, false, false);
            tx
        } 
        else {
            (tx: Option[TxOutput], _) = findAsReferenceOrOutput(asset.mph, asset.token_name, 1, ctx, false, false);
            tx
    });
    assert(output != Option[TxOutput]::None, "Asset not found in inputs");
    output.unwrap().datum
}

func assetIsValid(asset_type:String, pz_data: Map[String]Data, approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int, ctx: ScriptContext) -> (Bool, Option[Credential], OutputDatum) {
    if (pz_data.find_key_safe((key: String) -> { key == asset_type + "_asset" }) != Option[String]::None) {
        asset: AssetClass = parseAsset(ByteArray::from_data(pz_data.get(asset_type + "_asset")));

        // Is the asset passed in as an input/reference?
        (asset_output: Option[TxOutput], _) = findAsReferenceOrOutput(asset.mph, asset.token_name, 1, ctx, false, false);
        assert(asset_output != Option[TxOutput]::None, "Required " + asset_type + " input/reference missing");

        // Get datum if CIP-68
        asset_datum: OutputDatum = getAssetDatum(asset, ctx);

        approved: Map[ByteArray]Map[ByteArray][]Int = approvers.get(asset_type + "_approvers");

        assert(approved.any((pol: ByteArray, pattern: Map[ByteArray][]Int) -> Bool {
            // print(pol.serialize().show() + " = " + asset.mph.serialize().show() + " & " + asset.token_name.show());
            pol.serialize() == asset.mph.serialize() && pattern.any((p: ByteArray, _) -> Bool { asset.token_name.starts_with(p) })
        }), asset_type + " is not in the approved list");
        (true, Option[Credential]::Some{asset_output.unwrap().address.credential}, asset_datum)
    }
    else {
        (true, Option[Credential]::None, OutputDatum::new_none())
    }

}
func getAndValidateDesignerAssets(pz_data: Map[String]Data, owner_cred: Credential, approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int, ctx: ScriptContext, throw_errors:Bool) -> (OutputDatum, OutputDatum, Credential, Bool) {
    // handle, pfp, and bg can be ref_input or output
    // handle determines the address creds, all others must match 
    // We know we have a signature from the owner.

    // print("REDEEMER: " + pz_data.serialize().show());
    (pfp_valid: Bool, pfp_cred: Option[Credential], pfp_datum_opt: OutputDatum) = assetIsValid("pfp", pz_data, approvers, ctx);
    (bg_valid: Bool, bg_cred: Option[Credential], bg_datum_opt: OutputDatum) = assetIsValid("bg", pz_data, approvers, ctx);
    pfp_cred_valid: Bool = pfp_cred == Option[Credential]::None || pfp_cred.unwrap() == owner_cred;
    bg_cred_valid: Bool = bg_cred == Option[Credential]::None || bg_cred.unwrap() == owner_cred;
    if (throw_errors) {
        assert(pfp_valid && bg_valid, "Designer assets are not valid");
        assert(pfp_cred_valid, "PFP address not the same as handle");
        assert(bg_cred_valid, "BG address not the same as handle");
        (pfp_datum_opt, bg_datum_opt, owner_cred, true)
    }
    else {
        (pfp_datum_opt, bg_datum_opt, owner_cred, pfp_valid && bg_valid && pfp_cred_valid && bg_cred_valid)
    }
}

func checkRequiredPfPs(pfp_asset: Option[Data], bg_datum_opt:OutputDatum, owner_cred:Credential, ctx: ScriptContext, throw_errors: Bool) -> Bool {
    if (bg_datum_opt != OutputDatum::new_none()) {
        bg_datum = Datum::from_data(bg_datum_opt.get_inline_data());
        defaults: Map[String]Data = bg_datum.extra;
        require_pfp_collections_opt: Option[Data] = defaults.get_safe("require_pfp_collections");
        require_pfp_attributes_opt: Option[Data] = defaults.get_safe("require_pfp_attributes");
        require_pfp_displayed_opt: Option[Data] = defaults.get_safe("require_pfp_displayed");
        required_pfp_present: Bool = (require_pfp_collections_opt == Option[Data]::None || []ByteArray::from_data(require_pfp_collections_opt.unwrap()).any((asset_bytes: ByteArray) -> Bool {
            policy_id: MintingPolicyHash = MintingPolicyHash::new(asset_bytes.slice(0, 28));
            asset_name: ByteArray = asset_bytes.slice(28, asset_bytes.length);
            (output: Option[TxOutput], asset_opt: Option[AssetClass]) = findAsReferenceOrOutput(policy_id, asset_name, 1, ctx, true, false);
            asset: AssetClass = asset_opt.unwrap();
            output != Option[TxOutput]::None && output.unwrap().address.credential == owner_cred
                && (
                    if (asset.token_name.starts_with(LBL_222) || asset.token_name.starts_with(LBL_444)) {
                        required_attributes_present: Bool = (require_pfp_attributes_opt == Option[Data]::None || []ByteArray::from_data(require_pfp_attributes_opt.unwrap()).all((attr_bytes: ByteArray) -> Bool {
                            attr: String = attr_bytes.decode_utf8();
                            nft: Map[String]Data = Datum::from_data(getAssetDatum(asset, ctx).get_inline_data()).nft;
                            attributes: Map[String]Data = nft.get_safe("attributes").switch{ att: Some => Map[String]Data::from_data(att.some), else => nft };
                            found_att: Option[String] = attributes.find_key_safe((key: String) -> Bool { attr.starts_with(key + ":") });
                            found_att != Option[String]::None && attr.ends_with(":" + attributes.get(found_att.unwrap()).switch{
                                int: Int => int.show(),
                                b: ByteArray => b.decode_utf8(),
                                else => ""
                            })
                        }));
                        if (throw_errors) {
                            assert(required_attributes_present, "Required PFP attribute not present on PFP asset");
                            true
                        }
                        else {required_attributes_present}
                    }
                    // We can't check CIP-25 so just approve it
                    else {true}
                )
                && (
                    pfp_is_displayed: Bool = (require_pfp_displayed_opt == Option[Data]::None 
                        || Int::from_data(require_pfp_displayed_opt.unwrap()).to_bool() == false
                        || (Int::from_data(require_pfp_displayed_opt.unwrap()).to_bool()
                            && pfp_asset != Option[Data]::None && ByteArray::from_data(pfp_asset.unwrap()).starts_with(asset_bytes)));
                    print(pfp_asset.unwrap().serialize().show() + " = " + asset_bytes.show());
                    if (throw_errors) {
                        assert(pfp_is_displayed, "Required PFP isn't displayed in Handle");
                        true
                    }
                    else {pfp_is_displayed}
                )
        }));
        if (throw_errors) {
            assert(required_pfp_present, "Required PFP is not present in inputs");
            true
        }
        else {required_pfp_present}
    }
    else {
        true
    }
}

func designerSettingsAreValid(pz_data:Map[String]Data, designer: Map[String]Data, bg_datum_opt:OutputDatum, owner_cred:Credential, ctx: ScriptContext) -> Bool {
    if (bg_datum_opt != OutputDatum::new_none()) {
        bg_datum = Datum::from_data(bg_datum_opt.get_inline_data());
        defaults: Map[String]Data = bg_datum.extra;
        force: Option[Data] = defaults.get_safe("force_creator_settings");
        areRequiredPfpsPresent: Bool = checkRequiredPfPs(pz_data.get_safe("pfp_asset"), bg_datum_opt, owner_cred, ctx, true);
        if (force != Option[Data]::None && Int::from_data(force.unwrap()).to_bool() == true) {
            bg_border_colors: Option[Data] = defaults.get_safe("bg_border_colors");
            bg_border_color: ByteArray = ByteArray::from_data(designer.get("bg_border_color"));
            assert(bg_border_colors == Option[Data]::None || []ByteArray::from_data(bg_border_colors.unwrap()).find_safe((color:ByteArray) -> Bool { 
                color == bg_border_color
            }) != Option[ByteArray]::None, "bg_border_color is not set correctly");
            
            pfp_border_colors: Option[Data] = defaults.get_safe("pfp_border_colors");
            pfp_border_color: ByteArray = ByteArray::from_data(designer.get("pfp_border_color"));
            assert(pfp_border_colors == Option[Data]::None || []ByteArray::from_data(pfp_border_colors.unwrap()).find_safe((color:ByteArray) -> Bool {
                 color == pfp_border_color 
            }) != Option[ByteArray]::None, "pfp_border_color is not set correctly");
            
            text_ribbon_gradient_opt: Option[Data] = defaults.get_safe("text_ribbon_gradient");
            text_ribbon_gradient_x: String = text_ribbon_gradient_opt.switch{None => {""}, Some => { String::from_data(text_ribbon_gradient_opt.unwrap()) }};
            text_ribbon_gradient: String = String::from_data(designer.get("text_ribbon_gradient"));
            assert(text_ribbon_gradient_x == text_ribbon_gradient, "text_ribbon_gradient is not set correctly");
            
            text_ribbon_colors_opt: Option[Data] = defaults.get_safe("text_ribbon_colors");
            text_ribbon_colors_x: []ByteArray = text_ribbon_colors_opt.switch{None => {[]ByteArray{}}, Some => {[]ByteArray::from_data(text_ribbon_colors_opt.unwrap())}};
            text_ribbon_colors: []ByteArray = []ByteArray::from_data(designer.get("text_ribbon_colors"));
            assert(((text_ribbon_gradient_x == "radial" || text_ribbon_gradient_x.starts_with("linear")) 
                        && text_ribbon_colors_x == text_ribbon_colors) 
                    || text_ribbon_colors_x == text_ribbon_colors
                    || text_ribbon_colors_x.find_safe((color:ByteArray) -> Bool {color == text_ribbon_colors.get_singleton()}) != Option[ByteArray]::None, "text_ribbon_colors is not set correctly");
            
            font_color_opt: Option[Data] = defaults.get_safe("font_colors");
            font_color_x: ByteArray = font_color_opt.switch{None => {#ffffff}, Some => {ByteArray::from_data(font_color_opt.unwrap())}};
            font_color: ByteArray = ByteArray::from_data(designer.get("font_color"));
            assert(font_color_x == font_color, "font_color is not set correctly");

            font_shadow_colors: Option[Data] = defaults.get_safe("font_shadow_colors");
            font_shadow_color: ByteArray = ByteArray::from_data(designer.get("font_shadow_color"));
            assert(font_shadow_colors == Option[Data]::None || []ByteArray::from_data(font_shadow_colors.unwrap()).find_safe((color:ByteArray) -> Bool {
                color == font_shadow_color
            }) != Option[ByteArray]::None, "font_shadow_color is not set correctly");

            font_shadow_size_opt: Option[Data] = defaults.get_safe("font_shadow_size");
            font_shadow_size_x: []Int = font_shadow_size_opt.switch{None => {[]Int{}}, Some => {[]Int::from_data(font_shadow_size_opt.unwrap())}};
            font_shadow_size: []Int = []Int::from_data(designer.get("font_shadow_size"));
            assert(font_shadow_size_opt == Option[Data]::None || font_shadow_size_x == font_shadow_size, "font_shadow_size is not set correctly");

            font_opt: Option[Data] = defaults.get_safe("font");
            font_x: String = font_opt.switch{None => {""}, Some => {String::from_data(font_opt.unwrap())}};
            font: String = String::from_data(designer.get("font"));
            assert(font_x == font, "font is not set correctly");

            qr_bg_color_opt: Option[Data] = defaults.get_safe("qr_bg_color");
            qr_bg_color_x: ByteArray = qr_bg_color_opt.switch{None => {#}, Some => {ByteArray::from_data(qr_bg_color_opt.unwrap())}};
            qr_bg_color: ByteArray = ByteArray::from_data(designer.get("qr_bg_color"));
            assert(qr_bg_color_opt == Option[Data]::None || qr_bg_color_x == qr_bg_color, "qr_bg_color is not set correctly");

            qr_inner_eye_opt: Option[Data] = defaults.get_safe("qr_inner_eye");
            qr_inner_eye_x: String = qr_inner_eye_opt.switch{None => {""}, Some => {String::from_data(qr_inner_eye_opt.unwrap())}};
            qr_inner_eye: String = String::from_data(designer.get("qr_inner_eye"));
            assert(qr_inner_eye_opt == Option[Data]::None && qr_inner_eye.starts_with("square,") || qr_inner_eye_x == qr_inner_eye, "qr_inner_eye is not set correctly");

            qr_outer_eye_opt: Option[Data] = defaults.get_safe("qr_outer_eye");
            qr_outer_eye_x: String = qr_outer_eye_opt.switch{None => {""}, Some => {String::from_data(qr_outer_eye_opt.unwrap())}};
            qr_outer_eye: String = String::from_data(designer.get("qr_outer_eye"));
            assert(qr_outer_eye_opt == Option[Data]::None && qr_outer_eye.starts_with("square,") || qr_outer_eye_x == qr_outer_eye, "qr_outer_eye is not set correctly");

            qr_dot_opt: Option[Data] = defaults.get_safe("qr_dot");
            qr_dot_x: String = qr_dot_opt.switch{None => {""}, Some => {String::from_data(qr_dot_opt.unwrap())}};
            qr_dot: String = String::from_data(designer.get("qr_dot"));
            assert(qr_dot_opt == Option[Data]::None && qr_dot.starts_with("square,") || qr_dot_x == qr_dot, "qr_dot is not set correctly");

            pfp_zoom_opt: Option[Data] = defaults.get_safe("pfp_zoom");
            pfp_zoom_x: Int = pfp_zoom_opt.switch{None => {100}, Some => {Int::from_data(pfp_zoom_opt.unwrap())}};
            pfp_zoom: Int = Int::from_data(designer.get("pfp_zoom"));
            assert(pfp_zoom_x == pfp_zoom, "pfp_zoom is not set correctly");
            
            pfp_offset_opt: Option[Data] = defaults.get_safe("pfp_offset");
            pfp_offset_x: []Int = pfp_offset_opt.switch{None => []Int{0,0}, Some => {[]Int::from_data(pfp_offset_opt.unwrap())}};
            pfp_offset: []Int = []Int::from_data(designer.get("pfp_offset"));
            assert(pfp_offset_x == pfp_offset, "pfp_offset is not set correctly");

            assert(areRequiredPfpsPresent, "Background doesn't pass required PFP settings");
            true
        }
        else {
            assert(areRequiredPfpsPresent, "Background doesn't pass required PFP settings");
            true
        }
    }
    else {
        true
    }
}

func immutablesAreUnchanged(prev_nft: Datum, new_nft: Datum) -> Bool {
    prev_nft.extra.get("standard_image") == new_nft.extra.get("standard_image")
    && prev_nft.nft.get("name") == new_nft.nft.get("name")
    && prev_nft.nft.get("mediaType") == new_nft.nft.get("mediaType")
    && prev_nft.nft.get("og") == new_nft.nft.get("og")
    && prev_nft.nft.get("og_number") == new_nft.nft.get("og_number")
    && prev_nft.nft.get("rarity") == new_nft.nft.get("rarity")
    && prev_nft.nft.get("length") == new_nft.nft.get("length")
    && prev_nft.nft.get("characters") == new_nft.nft.get("characters")
    && prev_nft.nft.get("numeric_modifiers") == new_nft.nft.get("numeric_modifiers")
}

func checkBrightness() -> Bool {true}

func migrationSignersAreValid(ctx: ScriptContext, settings: PzSettings) -> Bool {
    // Check if Owner Signature is required
    assert(ctx.tx.signatories.length > 0 && 
        ctx.tx.signatories.all((signer: PubKeyHash) -> Bool {
            settings.admin_creds.any((admin: ByteArray) -> Bool {signer == PubKeyHash::new(admin)})
        }), "Required admin signer(s) not present");
        true
}

func pzDatumIsValid(
    owner_token: TxOutput, 
    reference_token: TxOutput,
    settings: PzSettings, 
    approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int, 
    ctx: ScriptContext) -> Bool {
        // check last_update_address == owner_222.address
        new_datum = Datum::from_data(reference_token.datum.get_inline_data());
        pz_data: Map[String]Data = new_datum.extra;
        assert(Address::from_data(pz_data.get("last_update_address")) == owner_token.address, "last_update_address does not match Handle address");
        // set provider validated - if provider in signatories ? validated == #provider_sig : validated == #
        validated_by = ByteArray::from_data(pz_data.get("validated_by"));
        provider: Option[PubKeyHash] = ctx.tx.signatories.find_safe((signer: PubKeyHash) -> Bool { settings.pz_providers.any((cred: ByteArray, _) -> Bool {signer == PubKeyHash::new(cred) && cred == validated_by})});
        assert((validated_by == # && provider == Option[PubKeyHash]::None) || provider != Option[PubKeyHash]::None, "validated_by is not set correctly");

        pfp_asset_opt: Option[Data] = pz_data.get_safe("pfp_asset");
        pfp_asset: AssetClass = pfp_asset_opt.switch{Some{a} => parseAsset(ByteArray::from_data(a)), else => AssetClass::new(MintingPolicyHash::new(#), #)};

        assert(pfp_asset_opt == Option[Data]::None || approvers.get("pfp_approvers").any((pol: ByteArray, pattern: Map[ByteArray][]Int) -> Bool {
            pol.serialize() == pfp_asset.mph.serialize() && pattern.any((p: ByteArray, props: []Int) -> Bool {
                pfp_asset.token_name.starts_with(p) 
                    && Int::from_data(pz_data.get("trial")) == props.get(1)
                    && Int::from_data(pz_data.get("nsfw")) == props.get(0)
            })
        }), "pfp_policy is in trial or nsfw, but Handle is not");

        bg_asset_opt: Option[Data] = pz_data.get_safe("bg_asset");
        bg_asset: AssetClass = bg_asset_opt.switch{Some{a} => parseAsset(ByteArray::from_data(a)), else => AssetClass::new(MintingPolicyHash::new(#), #)};

        assert(bg_asset_opt == Option[Data]::None || approvers.get("bg_approvers").any((pol: ByteArray, pattern: Map[ByteArray][]Int) -> Bool {
            pol.serialize() == bg_asset.mph.serialize() && pattern.any((p: ByteArray, props: []Int) -> Bool {
                bg_asset.token_name.starts_with(p) 
                    && Int::from_data(pz_data.get("trial")) == props.get(1)
                    && Int::from_data(pz_data.get("nsfw")) == props.get(0)
            })
        }), "bg_policy is in trial or nsfw, but Handle is not");

        assert(bg_asset_opt == Option[Data]::None || (
                bg_datum: OutputDatum = getAssetDatum(bg_asset, ctx);
                bg_datum == OutputDatum::new_none() || String::from_data(Datum::from_data(bg_datum.get_inline_data()).nft.get("image")) == String::from_data(pz_data.get("bg_image"))
            ), "bg_image doesn't match bg_asset datum");

        assert(pfp_asset_opt == Option[Data]::None || (
                pfp_datum: OutputDatum = getAssetDatum(pfp_asset, ctx);
                pfp_datum == OutputDatum::new_none() || String::from_data(Datum::from_data(pfp_datum.get_inline_data()).nft.get("image")) == String::from_data(pz_data.get("pfp_image"))
            ), "pfp_image doesn't match pfp_asset datum");

        true
}

func main(datum: Datum, redeemer: Redeemer, ctx: ScriptContext) -> Bool {
    redeemer.switch {
        pz: PERSONALIZE => {
            (settings: PzSettings, approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int) = checkInputsAndLoadSettings(ctx);
            (owner_token: TxOutput, reference_token: TxOutput) = getHandleTokens(pz.handle, ctx, true);
            new_datum = Datum::from_data(reference_token.datum.get_inline_data());
            assert(isValidContract(ctx.get_current_validator_hash(), settings) 
                && feesArePaid(ctx, settings, pz.handle) 
                && cborHashesToCid(pz.designer, reference_token)
                && immutablesAreUnchanged(datum, new_datum)
                , "Contract failed validation");

            (_, bg_datum:OutputDatum, _, _) = getAndValidateDesignerAssets(new_datum.extra, owner_token.address.credential, approvers, ctx, true);
            assert(pzDatumIsValid(owner_token, reference_token, settings, approvers, ctx)
                && designerSettingsAreValid(new_datum.extra, pz.designer, bg_datum, owner_token.address.credential, ctx) && checkBrightness(), "Personalization settings checks failed");
            true
        },
        migrate: MIGRATE => {
            settings: PzSettings = loadPzSettings(ctx);
            (reference_token: Option[TxOutput], _) = findAsReferenceOrOutput(HANDLE_POLICY, LBL_100 + (migrate.handle.encode_utf8()), 1, ctx, false, false);
            assert(datum == Datum::from_data(reference_token.unwrap().datum.get_inline_data())
                && isValidContract(reference_token.unwrap().address.credential.switch{v: Validator => v.hash, else => error("Not a valid migration")}, settings) 
                && migrationSignersAreValid(ctx, settings), "Not a valid migration");
            true            
        },
        reset: RESET_IMAGE => {
            (settings: PzSettings, approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int) = checkInputsAndLoadSettings(ctx);
            (owner_token: TxOutput, reference_token: TxOutput) = getHandleTokens(reset.handle, ctx, true);
            assert(isValidContract(ctx.get_current_validator_hash(), settings) 
                && cborHashesToCid(reset.designer, reference_token)
                && immutablesAreUnchanged(datum, Datum::from_data(reference_token.datum.get_inline_data()))
                , "Contract failed validation");
            
            (_, bg_datum:OutputDatum, _, designer_assets_valid:Bool) = getAndValidateDesignerAssets(reset.designer, owner_token.address.credential, approvers, ctx, false);
            assert(pzDatumIsValid(owner_token, reference_token, settings, approvers, ctx), "Personalization settings checks failed");

            new_datum = Datum::from_data(reference_token.datum.get_inline_data());
            pz_data: Map[String]Data = new_datum.extra;
            require_pfp_valid: Bool = checkRequiredPfPs(pz_data.get_safe("pfp_asset"), bg_datum, owner_token.address.credential, ctx, false);
            signed_by_provider: Bool = ctx.tx.signatories.any((signer: PubKeyHash) -> Bool { settings.pz_providers.any((cred: ByteArray, _) -> Bool {signer == PubKeyHash::new(cred)})});
            prev_datum_extra: Map[String]Data = datum.extra;
            address_changed_with_socials: Bool = ((prev_datum_extra.get("last_update_address") != pz_data.get("last_update_address")) && String::from_data(prev_datum_extra.get("socials")) != "");

            // Only allow reset if designer settings are off or last_update_address doesn't match
            if (designer_assets_valid == false || require_pfp_valid == false || address_changed_with_socials || signed_by_provider) {

                pz_datum_is_reset: Bool = (
                    pz_data.get("image") == pz_data.get("standard_image")
                    && pz_data.get("image_hash") == pz_data.get("standard_image_hash")
                    && ByteArray::from_data(pz_data.get("bg_asset")) == #
                    && ByteArray::from_data(pz_data.get("pfp_asset")) == #
                    && String::from_data(pz_data.get("pfp_image")) == ""
                    && String::from_data(pz_data.get("bg_image")) == "");

                designer_is_reset: Bool = (ByteArray::from_data(reset.designer.get("pfp_border_color")) == #
                    && String::from_data(reset.designer.get("font")) == ""
                    && ByteArray::from_data(reset.designer.get("font_color")) == #ffffff
                    && ByteArray::from_data(reset.designer.get("font_shadow_color")) == #
                    && []ByteArray::from_data(reset.designer.get("text_ribbon_colors")) == []ByteArray{}
                    && ByteArray::from_data(reset.designer.get("bg_color")) == #
                    && ByteArray::from_data(reset.designer.get("bg_border_color")) == #
                    && String::from_data(reset.designer.get("qr_link")) == ""
                    && Map[String]String::from_data(reset.designer.get("socials")) == Map[String]String{});

                pz_datum_is_reset && designer_is_reset
            }
            else {
                error("Reset is not valid or not authrized")
            }
        },
        else => {
            false
        }
    }
}

// TODO: Required Fields Present
// TODO: required_pfp count (SundaeSwap use case)
// TODO: Accessories/Premium elements (special ribbons, borders, holiday accouterments, etc.)
// TODO: Badges - can display up to X in a certain position
// TODO: Support either pubkeys or validatorkeys for bg_100, pfp_100, or provider tokens