spending handles_personalization

enum Redeemer {
    PERSONALIZE { 
        handle: String
        designer: Map[String]Data
    }
    MIGRATE { 
        handle: String
    }
    RESET { 
        handle: String
    }
    RETURN_TO_SENDER
}

struct PzSettings {
    treasury_fee: Int
    treasury_cred: ByteArray
    pz_min_fee: Int
    pz_providers: Map[ByteArray]ByteArray
    valid_contracts: []ByteArray
    admin_creds: []ByteArray
    settings_cred: ByteArray
}

enum Datum {
    CIP68 {
        nft: Map[String]Data
        version: Int
        extra: Map[String]Data
    }
}

const HANDLE_POLICY: MintingPolicyHash = MintingPolicyHash::new(#f0ff48bbb7bbe9d59a40f1ce90e9e9d0ff5002ec48f232b49ca0fb9a)
const LBL_444: ByteArray = #001bc280 // (444)bigirishlion
const LBL_222: ByteArray = #000de140 // (222)bigirishlion
const LBL_100: ByteArray = #000643b0 // (100)bigirishlion
const PZ_HANDLE: AssetClass = AssetClass::new(HANDLE_POLICY, LBL_222 + ("pz_settings".encode_utf8()))

func policyAndAssetSearch(output: TxOutput, policy: MintingPolicyHash, name: ByteArray, amount: Int, starts_with: Bool) -> Option[AssetClass] {
    output.value.to_map().fold_lazy((key: MintingPolicyHash, value: Map[ByteArray]Int, next: () -> Option[AssetClass]) -> {
        asset_name: Option[ByteArray] = value.find_key_safe((k: ByteArray) -> Bool { if (starts_with) { k.starts_with(name) } else { k == name } });
        if (policy == key && asset_name != Option[ByteArray]::None && value.get(asset_name.unwrap()) >= amount) {
            Option[AssetClass]::Some{AssetClass::new(key, asset_name.unwrap())}
        } else { next() }
    }, Option[AssetClass]::None)
}

func findAsReferenceOrOutput(policy: MintingPolicyHash, name: ByteArray, amount: Int, ctx: ScriptContext, starts_with: Bool, check_sig: Bool) -> (Option[TxOutput], Option[AssetClass]) {
    (tx: Option[TxOutput], asset: Option[AssetClass]) = ctx.tx.ref_inputs.fold2_lazy(
        (input: TxInput, next: () -> (Option[TxOutput], Option[AssetClass])) -> {
            a: Option[AssetClass] = policyAndAssetSearch(input.output, policy, name, amount, starts_with);
            b: Bool = if (check_sig) {
            ctx.tx.signatories.any((signer: PubKeyHash) -> {
                input.output.address.credential == Credential::new_pubkey(signer)
            })
            } else {true};

            if (a != Option[AssetClass]::None && b) {
                (Option[TxOutput]::Some{input.output}, a)
            } else { 
            next() 
            }
        }, 
        Option[TxOutput]::None, Option[AssetClass]::None
    );
    tx.switch {
        Some => (tx, asset), 
        else => {
            ctx.tx.outputs.fold2_lazy(
                (output: TxOutput, next: () -> (Option[TxOutput], Option[AssetClass])) -> {
                    a: Option[AssetClass] = policyAndAssetSearch(output, policy, name, amount, starts_with);
                    if (a != Option[AssetClass]::None) {
                        (Option[TxOutput]::Some{output}, a)
                    } else { next() }
            }, Option[TxOutput]::None, Option[AssetClass]::None)
        }
    }
}

func isValidContract(hash: ValidatorHash, settings: PzSettings) -> Bool {
    assert(settings.valid_contracts.any((cred: ByteArray) -> Bool {
        Credential::new_validator(hash) == Credential::new_validator(ValidatorHash::new(cred))
    }), "Contract not found in valid contracts list");
    
    true
}

func getHandleTokens(handle: String, settings: PzSettings, ctx: ScriptContext, check_sig: Bool) -> (TxOutput, TxOutput) {
    (reference_token: Option[TxOutput], _) = findAsReferenceOrOutput(HANDLE_POLICY, LBL_100 + (handle.encode_utf8()), 1, ctx, false, false);
    (owner_token: Option[TxOutput], _) = findAsReferenceOrOutput(HANDLE_POLICY, LBL_222 + (handle.encode_utf8()), 1, ctx, false, check_sig);
    assert(owner_token != Option[TxOutput]::None, "Handle input not present");
    assert(reference_token != Option[TxOutput]::None, "Reference Token input not present");
    assert(isValidContract(reference_token.unwrap().address.credential.switch{v: Validator => v.hash, else => error("Invalid contract address")}, settings), "Reference Token not returned to contract");
    (owner_token.unwrap(), reference_token.unwrap())
}

func loadPzSettings(ctx: ScriptContext) -> PzSettings {
    pz_input: Option[TxInput] = ctx.tx.ref_inputs.find_safe((input: TxInput) -> Bool { 
        input.output.value.get_safe(PZ_HANDLE) == 1
    });
    assert(pz_input != Option[TxInput]::None, "pz_settings reference input not present");
    settings = PzSettings::from_data(pz_input.unwrap().output.datum.get_inline_data());
    assert(pz_input.unwrap().output.address.credential == Credential::new_validator(ValidatorHash::new(settings.settings_cred)), "pz_settings reference input not from ADA Handle");

    settings
}

func hasValueUnWrapped(data: Option[Data]) -> (Bool, Data) {
    if (data == Option[Data]::None) {
        (false, OutputDatum::new_inline(#).data)
    }
    else{
        data.unwrap().switch {
            Int => {(true, data.unwrap())},
            b: ByteArray => {(b != #, data.unwrap())},
            _ => {(false, OutputDatum::new_inline(#).data)}
        }
    }
}

func getApproverDatum(handle:String, settings: PzSettings, ctx: ScriptContext) -> Map[ByteArray]Map[ByteArray][]Int {
    input: Option[TxInput] = ctx.tx.ref_inputs.find_safe((input: TxInput) -> Bool { 
        (input.output.value.get_safe(AssetClass::new(HANDLE_POLICY, LBL_222 + (handle.encode_utf8()))) == 1 
        || (input.output.value.contains_policy(HANDLE_POLICY) && input.output.value.get_policy(HANDLE_POLICY).any((asset_name: ByteArray, count: Int) -> Bool {
                asset_name.ends_with(("@" + handle).encode_utf8()) && count == 1
        })))
        && ((settings.pz_providers.any((_, cred: ByteArray) -> Bool { input.output.address.credential.switch{
            p: PubKey => {p == Credential::new_pubkey(PubKeyHash::new(cred))}, 
            v: Validator => {v == Credential::new_validator(ValidatorHash::new(cred))}}}))
        || input.output.address.credential == Credential::new_validator(ValidatorHash::new(settings.settings_cred)))
    });
    assert(input != Option[TxInput]::None, handle + " reference input not present or not from valid provider");
    Map[ByteArray]Map[ByteArray][]Int::from_data(input.unwrap().output.datum.get_inline_data())
}

func checkInputsAndLoadSettings(ctx: ScriptContext) -> (PzSettings, Map[String]Map[ByteArray]Map[ByteArray][]Int) {
    // TODO: Add @acc_policy_ids (for accessories directly on the Handle - image borders [pfp/bg], santa/leprechaun hats, halloween bats, etc)
    settings: PzSettings = loadPzSettings(ctx);
    (settings, Map[String]Map[ByteArray]Map[ByteArray][]Int{"bg_approvers": getApproverDatum("bg_policy_ids", settings, ctx), "pfp_approvers": getApproverDatum("pfp_policy_ids", settings, ctx)})
}

func feesArePaid(ctx: ScriptContext, settings: PzSettings, handle: String) -> Bool {
    // Treasury fee paid
    assert(ctx.tx.outputs.any((output: TxOutput) -> Bool {
        // Check that datum is attached so it can be spent (also guard against double satisfaction)
        output.address.credential == Credential::new_validator(ValidatorHash::new(settings.treasury_cred))
            && output.value.contains(Value::lovelace(settings.treasury_fee))
            && output.datum.get_inline_data().switch{b: ByteArray => { b == handle.encode_utf8() }, else => {false}}
    }), "Handle treasury fee unpaid");

    // PZ provider fee paid
    assert(ctx.tx.outputs.any((output: TxOutput) -> Bool {
        // Check that datum is attached so it can be spent (also guard against double satisfaction)
        settings.pz_providers.any((_, cred: ByteArray) -> Bool { output.address.credential.switch{
            p: PubKey => {p == Credential::new_pubkey(PubKeyHash::new(cred))}, 
            v: Validator => {v == Credential::new_validator(ValidatorHash::new(cred))}}})
        && output.value.contains(Value::lovelace(settings.pz_min_fee))
        && output.datum.get_inline_data().switch{b: ByteArray => { b == handle.encode_utf8() }, else => {false}}
    }), "Personalization provider not found or fee unpaid");

    true
}

func cborHashesToCid(designer: Map[String]Data, reference_token: TxOutput) -> Bool {
    //print("REDEEMER: " + designer.serialize().show());
    ipfs = ByteArray::from_data(Datum::CIP68::from_data(reference_token.datum.get_inline_data()).extra.get("designer"));
    multihash: ByteArray = Int::from_base58(ipfs.slice(8, ipfs.length).decode_utf8()).to_big_endian();  // This strips away "ipfs://z" (the z can be removed - see CID multiformats)
    // print("multihash = " + multihash.show());
    // print("designer.show() = " + designer.show());
    // print(content_hash.show() + " = " + designer.serialize().sha2().show());
    // This strips away 0x01 0x70 0x12 0x20 (CIDv1, base58, sha2, 32 bytes)
    assert(multihash.slice(4, multihash.length) == designer.serialize().sha2(), "Personalization designer settings hash doesn't match CID multihash");
    true
}

func parseAsset(asset_bytes: ByteArray) -> AssetClass {
    assert(asset_bytes.length > 28, "Asset is defined but not valid");
    policy = MintingPolicyHash::new(asset_bytes.slice(0, 28));
    AssetClass::new(policy, asset_bytes.slice(28, asset_bytes.length))
}

func getAssetDatum(asset: AssetClass, ctx: ScriptContext) -> OutputDatum {
    output: Option[TxOutput] = (
        if (asset.token_name.starts_with(LBL_222) || asset.token_name.starts_with(LBL_444)) {
            real_name: ByteArray = asset.token_name.slice(4, asset.token_name.length);
            (tx: Option[TxOutput], _) = findAsReferenceOrOutput(asset.mph, LBL_100 + real_name, 1, ctx, false, false);
            assert(tx != Option[TxOutput]::None, "Asset not found in inputs");
            assert(tx.unwrap().datum != OutputDatum::new_none(), "CIP-68 tokens require inline datum");
            tx
        } 
        else {
            (tx: Option[TxOutput], _) = findAsReferenceOrOutput(asset.mph, asset.token_name, 1, ctx, false, false);
            tx
    });
    assert(output != Option[TxOutput]::None, "Asset not found in inputs");
    output.unwrap().datum
}

func assetIsValid(asset_type:String, pz_data: Map[String]Data, approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int, ctx: ScriptContext) -> (Bool, Option[Credential], OutputDatum) {
    if (pz_data.find_key_safe((key: String) -> { key == asset_type + "_asset" }) != Option[String]::None) {
        asset: AssetClass = parseAsset(ByteArray::from_data(pz_data.get(asset_type + "_asset")));

        //print(asset.mph.show() + " " + asset.token_name.show());
        // Is the asset passed in as an input/reference?
        (asset_output: Option[TxOutput], _) = findAsReferenceOrOutput(asset.mph, asset.token_name, 1, ctx, false, false);
        assert(asset_output != Option[TxOutput]::None, "Required " + asset_type + " input/reference missing");

        assert(approvers.get(asset_type + "_approvers").any((pol: ByteArray, pattern: Map[ByteArray][]Int) -> Bool {
            // print(pol.serialize().show() + " = " + asset.mph.serialize().show() + " & " + asset.token_name.show());
            pol.serialize() == asset.mph.serialize() && pattern.any((p: ByteArray, _) -> Bool { asset.token_name.starts_with(p) })
        }), asset_type + " is not in the approved list");
        (true, Option[Credential]::Some{asset_output.unwrap().address.credential}, getAssetDatum(asset, ctx))
    }
    else {
        (true, Option[Credential]::None, OutputDatum::new_none())
    }

}

func getAndValidateDesignerAssets(pz_data: Map[String]Data, owner_cred: Credential, approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int, ctx: ScriptContext, throw_errors:Bool) -> (OutputDatum, Bool) {
    // handle, pfp, and bg can be ref_input or output
    // handle determines the address creds, all others must match 
    // We know we have a signature from the owner.

    // print("REDEEMER: " + pz_data.serialize().show());
    (pfp_valid: Bool, pfp_cred: Option[Credential], _) = assetIsValid("pfp", pz_data, approvers, ctx);
    (bg_valid: Bool, bg_cred: Option[Credential], bg_datum_opt: OutputDatum) = assetIsValid("bg", pz_data, approvers, ctx);
    pfp_cred_valid: Bool = pfp_cred == Option[Credential]::None || pfp_cred.unwrap() == owner_cred;
    bg_cred_valid: Bool = bg_cred == Option[Credential]::None || bg_cred.unwrap() == owner_cred;
    // print("pfp_cred_valid == " + pfp_cred_valid.show());
    if (throw_errors) {
        assert(pfp_valid && bg_valid, "Designer assets are not valid");
        assert(pfp_cred_valid, "PFP address not the same as handle");
        assert(bg_cred_valid, "BG address not the same as handle");
        (bg_datum_opt, true)
    }
    else {
        (bg_datum_opt, pfp_valid && bg_valid && pfp_cred_valid && bg_cred_valid)
    }
}

func checkRequiredPfPs(pfp_asset: Option[Data], bg_datum_opt:OutputDatum, owner_cred:Credential, ctx: ScriptContext, throw_errors: Bool) -> Bool {
    if (bg_datum_opt != OutputDatum::new_none()) {
        defaults: Map[String]Data = Datum::CIP68::from_data(bg_datum_opt.get_inline_data()).extra;
        required_sig_opt: Option[Data] = defaults.get_safe("required_signature");
        assert(required_sig_opt == Option[Data]::None || ctx.tx.signatories.any((sig: PubKeyHash) -> Bool{sig == PubKeyHash::new(ByteArray::from_data(required_sig_opt.unwrap()))}), "Required signature for background not present");
        require_pfp_collections_opt: Option[Data] = defaults.get_safe("require_pfp_collections");
        require_pfp_attributes_opt: Option[Data] = defaults.get_safe("require_pfp_attributes");
        require_pfp_displayed_opt: Option[Data] = defaults.get_safe("require_pfp_displayed");
        required_pfp_present: Bool = (require_pfp_collections_opt == Option[Data]::None || []ByteArray::from_data(require_pfp_collections_opt.unwrap()).any((asset_bytes: ByteArray) -> Bool {
            (output: Option[TxOutput], asset_opt: Option[AssetClass]) = findAsReferenceOrOutput(MintingPolicyHash::new(asset_bytes.slice(0, 28)), asset_bytes.slice(28, asset_bytes.length), 1, ctx, true, false);
            output != Option[TxOutput]::None && output.unwrap().address.credential == owner_cred
                && (
                    asset: AssetClass = asset_opt.unwrap();
                    if (asset.token_name.starts_with(LBL_222) || asset.token_name.starts_with(LBL_444)) {
                        required_attributes_present: Bool = (require_pfp_attributes_opt == Option[Data]::None || []ByteArray::from_data(require_pfp_attributes_opt.unwrap()).all((attr_bytes: ByteArray) -> Bool {
                            attr: String = attr_bytes.decode_utf8();
                            nft: Map[String]Data = Datum::CIP68::from_data(getAssetDatum(asset, ctx).get_inline_data()).nft;
                            attributes: Map[String]Data = nft.get_safe("attributes").switch{ att: Some => Map[String]Data::from_data(att.some), else => nft };
                            found_att: Option[String] = attributes.find_key_safe((key: String) -> Bool { attr.starts_with(key + ":") });
                            found_att != Option[String]::None && attr.ends_with(":" + attributes.get(found_att.unwrap()).switch{
                                int: Int => int.show(),
                                b: ByteArray => b.decode_utf8(),
                                else => ""
                            })
                        }));
                        if (throw_errors) {
                            assert(required_attributes_present, "Required PFP attribute not present on PFP asset");
                            true
                        }
                        else {required_attributes_present}
                    }
                    // We can't check CIP-25 so just approve it
                    else {true}
                )
                && (
                    pfp_is_displayed: Bool = (require_pfp_displayed_opt == Option[Data]::None 
                        || Int::from_data(require_pfp_displayed_opt.unwrap()).to_bool() == false
                        || (Int::from_data(require_pfp_displayed_opt.unwrap()).to_bool()
                            && pfp_asset != Option[Data]::None && ByteArray::from_data(pfp_asset.unwrap()).starts_with(asset_bytes)));
                    //print(pfp_asset.unwrap().serialize().show() + " = " + asset_bytes.show());
                    if (throw_errors) {
                        assert(pfp_is_displayed, "Required PFP isn't displayed in Handle");
                        true
                    }
                    else {pfp_is_displayed}
                )
        }));
        if (throw_errors) {
            assert(required_pfp_present, "Required PFP is not present in inputs");
            true
        }
        else {required_pfp_present}
    }
    else {
        true
    }
}

func propInDesignerArray(default: Option[Data], property: Option[Data], forced: Bool = false) -> Bool {
    (has_val: Bool, prop: Data) = hasValueUnWrapped(property);
    default == Option[Data]::None 
    || forced == false
    || (forced && has_val && []ByteArray::from_data(default.unwrap()).find_safe((val:ByteArray) -> Bool {val == ByteArray::from_data(prop)}) != Option[ByteArray]::None)
}

func propEqualsDesignerDefault(default: Option[Data], property: Option[Data], exclusive: Bool = false, forced: Bool = false) -> Bool {
    (has_val:Bool, _) = hasValueUnWrapped(property);
    default == property // This is good already
    || (exclusive && forced == false && has_val == false) // This can equal either the creator default or blank
    || default == Option[Data]::None && (exclusive == false || has_val == false ) // If creators don't define it and it is exclusive, it has to be blank
}

func exclusiveSettingsAreValid(defaults: Map[String]Data, designer: Map[String]Data, forced: Bool) -> Bool {
    assert(propEqualsDesignerDefault(defaults.get_safe("text_ribbon_gradient"), designer.get_safe("text_ribbon_gradient"), true, forced), "text_ribbon_gradient is not set correctly");
    assert(propEqualsDesignerDefault(defaults.get_safe("font_color"), designer.get_safe("font_color"), true, forced), "font_color is not set correctly");
    assert(propEqualsDesignerDefault(defaults.get_safe("font"), designer.get_safe("font"), true, forced), "font is not set correctly");
    assert(propEqualsDesignerDefault(defaults.get_safe("qr_image"), designer.get_safe("qr_image"), true, forced), "qr_image is not set correctly");

    (_, ribbon_gradient_val_data: Data) = hasValueUnWrapped(defaults.get_safe("text_ribbon_gradient"));
    ribbon_gradient_val: ByteArray = ByteArray::from_data(ribbon_gradient_val_data);
    creator_ribbon_colors_opt: Option[Data] = defaults.get_safe("text_ribbon_colors");
    no_creator_colors: Bool = creator_ribbon_colors_opt == Option[Data]::None;
    creator_ribbon_colors: []ByteArray = creator_ribbon_colors_opt.switch{None => {[]ByteArray{}}, Some => {[]ByteArray::from_data(creator_ribbon_colors_opt.unwrap())}};
    ribbon_colors: []ByteArray = designer.get_safe("text_ribbon_colors").switch{None => {[]ByteArray{}}, Some{d} => {[]ByteArray::from_data(d)}};
    gradient: Bool = ribbon_gradient_val == "radial".encode_utf8() || ribbon_gradient_val.starts_with("linear".encode_utf8());
    assert((no_creator_colors && ribbon_colors.length <= 1) // Creator didn't define, so any single ribbon color is fine
        || (forced == false && ribbon_colors.length <= 1) // Not forced, so any single ribbon color is fine
        || (gradient && creator_ribbon_colors == ribbon_colors) // If there is a gradient, then the colors must match
        || (gradient == false && ribbon_colors.length == 1 && creator_ribbon_colors.any((color: ByteArray) -> Bool {color == ribbon_colors.get_singleton()}))
    , "text_ribbon_colors is not set correctly");

    qr_inner_eye_opt: Option[Data] = defaults.get_safe("qr_inner_eye");
    qr_inner_eye_x: String = qr_inner_eye_opt.switch{None => {""}, Some{s} => {String::from_data(s)}};
    qr_inner_eye: String = designer.get_safe("qr_inner_eye").switch{None => {""}, Some{s} => {String::from_data(s)}};
    assert(qr_inner_eye_opt == Option[Data]::None && qr_inner_eye.starts_with("square,") 
        || (forced && qr_inner_eye_x == qr_inner_eye)
        || (forced == false && (qr_inner_eye.starts_with("square,") || qr_inner_eye_x == qr_inner_eye)), "qr_inner_eye is not set correctly");

    qr_outer_eye_opt: Option[Data] = defaults.get_safe("qr_outer_eye");
    qr_outer_eye_x: String = qr_outer_eye_opt.switch{None => {""}, Some{s} => {String::from_data(s)}};
    qr_outer_eye: String = designer.get_safe("qr_outer_eye").switch{None => {""}, Some{s} => {String::from_data(s)}};
    assert(qr_outer_eye_opt == Option[Data]::None && qr_outer_eye.starts_with("square,") 
        || (forced && qr_outer_eye_x == qr_outer_eye)
        || (forced == false && (qr_outer_eye.starts_with("square,") || qr_outer_eye_x == qr_outer_eye)), "qr_outer_eye is not set correctly");

    qr_dot_opt: Option[Data] = defaults.get_safe("qr_dot");
    qr_dot_x: String = qr_dot_opt.switch{None => {""}, Some{s} => {String::from_data(s)}};
    qr_dot: String = designer.get_safe("qr_dot").switch{None => {""}, Some{s} => {String::from_data(s)}};
    assert(qr_dot_opt == Option[Data]::None && qr_dot.starts_with("square,") 
        || (forced && qr_dot_x == qr_dot)
        || (forced == false && (qr_dot.starts_with("square,") || qr_dot_x == qr_dot)), "qr_dot is not set correctly");
    true
}

func designerSettingsAreValid(pz_data:Map[String]Data, designer: Map[String]Data, bg_datum_opt:OutputDatum, owner_cred:Credential, ctx: ScriptContext) -> Bool {
    defaults: Map[String]Data = (
        if (bg_datum_opt != OutputDatum::new_none()) {
            bg_datum: Datum::CIP68 = Datum::CIP68::from_data(bg_datum_opt.get_inline_data());
            bg_datum.extra
        }
        else {Map[String]Data{}}
    );
    forced: Bool = (
        force: Option[Data] = defaults.get_safe("force_creator_settings");
        force != Option[Data]::None && Int::from_data(force.unwrap()).to_bool() == true
    );
    areRequiredPfpsPresent: Bool = checkRequiredPfPs(pz_data.get_safe("pfp_asset"), bg_datum_opt, owner_cred, ctx, true);
    assert(areRequiredPfpsPresent, "Background doesn't pass required PFP settings");
    assert(propInDesignerArray(defaults.get_safe("bg_border_colors"), designer.get_safe("bg_border_color"), forced), "bg_border_color is not set correctly");
    assert(propInDesignerArray(defaults.get_safe("pfp_border_colors"), designer.get_safe("pfp_border_color"), forced), "pfp_border_color is not set correctly");
    assert(propInDesignerArray(defaults.get_safe("font_shadow_colors"), designer.get_safe("font_shadow_color"), forced), "font_shadow_color is not set correctly");
    assert(propEqualsDesignerDefault(defaults.get_safe("font_shadow_size"), designer.get_safe("font_shadow_size"), false, forced), "font_shadow_size is not set correctly");
    assert(propEqualsDesignerDefault(defaults.get_safe("pfp_zoom"), designer.get_safe("pfp_zoom"), false, forced), "pfp_zoom is not set correctly");
    assert(propEqualsDesignerDefault(defaults.get_safe("pfp_offset"), designer.get_safe("pfp_offset"), false, forced), "pfp_offset is not set correctly");
    assert(propEqualsDesignerDefault(defaults.get_safe("qr_bg_color"), designer.get_safe("qr_bg_color"), false, forced), "qr_bg_color is not set correctly");
    assert(exclusiveSettingsAreValid(defaults, designer, forced), "Exclusive settings are only for approved projects");
    true
}

func immutablesAreUnchanged(prev_nft: Datum::CIP68, new_nft: Datum::CIP68) -> Bool {
    assert(prev_nft.extra.get("standard_image") == new_nft.extra.get("standard_image")
    && prev_nft.extra.get("standard_image_hash") == new_nft.extra.get("standard_image_hash")
    && prev_nft.nft.get("name") == new_nft.nft.get("name")
    && prev_nft.nft.get("mediaType") == new_nft.nft.get("mediaType")
    && prev_nft.nft.get("og") == new_nft.nft.get("og")
    && prev_nft.nft.get("og_number") == new_nft.nft.get("og_number")
    && prev_nft.nft.get("rarity") == new_nft.nft.get("rarity")
    && prev_nft.nft.get("length") == new_nft.nft.get("length")
    && prev_nft.nft.get("characters") == new_nft.nft.get("characters")
    && prev_nft.nft.get("numeric_modifiers") == new_nft.nft.get("numeric_modifiers"), "Immutables have changed");
    true
}

func adminHasSignedTx(settings: PzSettings, ctx: ScriptContext) -> Bool {
    ctx.tx.signatories.length > 0 
    && ctx.tx.signatories.any((signer: PubKeyHash) -> Bool { signer == PubKeyHash::new(#4da965a049dfd15ed1ee19fba6e2974a0b79fc416dd1796a1f97f5e1) || settings.admin_creds.any((admin: ByteArray) -> Bool {signer == PubKeyHash::new(admin)}) })
}

func migrationSignersAreValid(migrate: Redeemer::MIGRATE, ctx: ScriptContext, settings: PzSettings, old_datum: Datum::CIP68) -> Bool {
    // Check if Owner Signature is required
    owner_sig_required: Bool = old_datum.extra.get_safe("migrate_sig_required").switch{Some{req} => {Int::from_data(req) != 0}, else => {false}};
    assert(adminHasSignedTx(settings, ctx), "Required admin signer(s) not present");
    if (owner_sig_required) {
        (owner_token: Option[TxOutput], _) = findAsReferenceOrOutput(HANDLE_POLICY, LBL_222 + (migrate.handle.encode_utf8()), 1, ctx, false, true);
        assert(owner_token != Option[TxOutput]::None, "Required owner signer not present");
        true
    }
    else {
        true
    }
}

func pzDatumIsValid(
    owner_token: TxOutput, 
    reference_token: TxOutput,
    approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int, 
    ctx: ScriptContext) -> Bool {
        // check last_update_address == owner_222.address
        pz_data: Map[String]Data = Datum::CIP68::from_data(reference_token.datum.get_inline_data()).extra;
        last_address_bytes = ByteArray::from_data(pz_data.get("last_update_address"));
        assert(Address::new(Credential::new_pubkey(PubKeyHash::new(last_address_bytes.slice(1,29))), (
            if (last_address_bytes.length > 29 ) { 
                Option[StakingCredential]::Some{StakingCredential::new_hash(StakingHash::new_stakekey(PubKeyHash::new(last_address_bytes.slice(29,57))))} 
            } 
            else {
                Option[StakingCredential]::None
            })) == owner_token.address, "last_update_address does not match Handle address");
        validated_by: Option[Data] = pz_data.get_safe("validated_by");
        assert(validated_by == Option[Data]::None || ctx.tx.signatories.any((signer: PubKeyHash) -> Bool {signer == PubKeyHash::new(ByteArray::from_data(validated_by.unwrap()))}), "validated_by is set but not signed");

        (pfp_asset_set: Bool, pfp_asset_data: Data) = hasValueUnWrapped(pz_data.get_safe("pfp_asset"));
        pfp_asset: AssetClass = (if (pfp_asset_set) {parseAsset(ByteArray::from_data(pfp_asset_data))} else {AssetClass::new(MintingPolicyHash::new(#), #)});
    
        assert(pfp_asset_set == false || approvers.get("pfp_approvers").any((pol: ByteArray, pattern: Map[ByteArray][]Int) -> Bool {
            pol.serialize() == pfp_asset.mph.serialize() && pattern.any((p: ByteArray, props: []Int) -> Bool {
                pfp_asset.token_name.starts_with(p) 
                    && Int::from_data(pz_data.get("trial")) == props.get(1)
                    && Int::from_data(pz_data.get("nsfw")) == props.get(0)
            })
        }), "Trial/NSFW flags set incorrectly (PFP)");
    
        (bg_asset_set: Bool, bg_asset_data: Data) = hasValueUnWrapped(pz_data.get_safe("bg_asset"));
        bg_asset: AssetClass = (if (bg_asset_set) {parseAsset(ByteArray::from_data(bg_asset_data))} else {AssetClass::new(MintingPolicyHash::new(#), #)});
    
        assert(bg_asset_set == false || approvers.get("bg_approvers").any((pol: ByteArray, pattern: Map[ByteArray][]Int) -> Bool {
            pol.serialize() == bg_asset.mph.serialize() && pattern.any((p: ByteArray, props: []Int) -> Bool {
                bg_asset.token_name.starts_with(p) 
                    && Int::from_data(pz_data.get("trial")) == props.get(1)
                    && Int::from_data(pz_data.get("nsfw")) == props.get(0)
            })
        }), "Trial/NSFW flags set incorrectly (BG)");

        (bg_image_set: Bool, bg_image_data: Data) = hasValueUnWrapped(pz_data.get_safe("bg_image"));
        assert((bg_asset_set && bg_image_set) || (bg_asset_set == false && bg_image_set == false), "bg_asset/bg_image mismatch");
        assert(bg_asset_set == false || (
            bg_datum: OutputDatum = getAssetDatum(bg_asset, ctx);
            bg_datum == OutputDatum::new_none() || String::from_data(Datum::CIP68::from_data(bg_datum.get_inline_data()).nft.get("image")) == ByteArray::from_data(bg_image_data).decode_utf8()), "bg_image doesn't match bg_asset datum");

        (pfp_image_set: Bool, pfp_image_data: Data) = hasValueUnWrapped(pz_data.get_safe("pfp_image"));
        assert((pfp_asset_set && pfp_image_set) || (pfp_asset_set == false && pfp_image_set == false), "pfp_asset/pfp_image mismatch");
        assert(pfp_asset_set == false || (
            pfp_datum: OutputDatum = getAssetDatum(pfp_asset, ctx);
            pfp_datum == OutputDatum::new_none() || String::from_data(Datum::CIP68::from_data(pfp_datum.get_inline_data()).nft.get("image")) == ByteArray::from_data(pfp_image_data).decode_utf8()), "pfp_image doesn't match pfp_asset datum");
    
        true
}

func main(datum: Datum, redeemer: Redeemer, ctx: ScriptContext) -> Bool {
    old_datum: Datum::CIP68 = datum.switch {
        d: CIP68 => d,
        else => error("Invalid input datum")
    };
    redeemer.switch {
        pz: PERSONALIZE => {
            (settings: PzSettings, approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int) = checkInputsAndLoadSettings(ctx);
            (owner_token: TxOutput, reference_token: TxOutput) = getHandleTokens(pz.handle, settings, ctx, true);
            new_datum: Datum::CIP68 = Datum::CIP68::from_data(reference_token.datum.get_inline_data());
            assert(isValidContract(ctx.get_current_validator_hash(), settings) 
                && feesArePaid(ctx, settings, pz.handle) 
                && cborHashesToCid(pz.designer, reference_token)
                && immutablesAreUnchanged(old_datum, new_datum)
                , "Contract failed validation");

            (bg_datum:OutputDatum, _) = getAndValidateDesignerAssets(new_datum.extra, owner_token.address.credential, approvers, ctx, true);
            assert(pzDatumIsValid(owner_token, reference_token, approvers, ctx)
                && designerSettingsAreValid(new_datum.extra, pz.designer, bg_datum, owner_token.address.credential, ctx), "Personalization settings checks failed");
            true
        },
        migrate: MIGRATE => {
            settings: PzSettings = loadPzSettings(ctx);
            (reference_token: Option[TxOutput], _) = findAsReferenceOrOutput(HANDLE_POLICY, LBL_100 + (migrate.handle.encode_utf8()), 1, ctx, false, false);
            //print(reference_token.serialize().show());
            new_datum: Datum::CIP68 = Datum::CIP68::from_data(reference_token.unwrap().datum.get_inline_data());
            assert(old_datum == new_datum
                && isValidContract(reference_token.unwrap().address.credential.switch{v: Validator => v.hash, else => error("Not a valid migration")}, settings) 
                && migrationSignersAreValid(migrate, ctx, settings, old_datum), "Not a valid migration");
            true            
        },
        reset: RESET => {
            (settings: PzSettings, approvers: Map[String]Map[ByteArray]Map[ByteArray][]Int) = checkInputsAndLoadSettings(ctx);
            (owner_token: TxOutput, reference_token: TxOutput) = getHandleTokens(reset.handle, settings, ctx, false);
            assert(isValidContract(ctx.get_current_validator_hash(), settings) 
                && immutablesAreUnchanged(old_datum, Datum::CIP68::from_data(reference_token.datum.get_inline_data()))
                , "Contract failed validation");
            
            (bg_datum:OutputDatum, designer_assets_valid:Bool) = getAndValidateDesignerAssets(old_datum.extra, owner_token.address.credential, approvers, ctx, false);
            assert(pzDatumIsValid(owner_token, reference_token, approvers, ctx), "Personalization settings checks failed");

            new_datum: Datum::CIP68 = Datum::CIP68::from_data(reference_token.datum.get_inline_data());
            pz_data: Map[String]Data = new_datum.extra;
            require_pfp_valid: Bool = checkRequiredPfPs(pz_data.get_safe("pfp_asset"), bg_datum, owner_token.address.credential, ctx, false);
            signed_by_provider: Bool = ctx.tx.signatories.any((signer: PubKeyHash) -> Bool { settings.pz_providers.any((cred: ByteArray, _) -> Bool {signer == PubKeyHash::new(cred)})});
            prev_datum_extra: Map[String]Data = old_datum.extra;
            address_changed_with_socials: Bool = ((prev_datum_extra.get("last_update_address") != pz_data.get("last_update_address")) && String::from_data(prev_datum_extra.get("socials")) != "");

            // Only allow reset if designer settings are off or last_update_address doesn't match or signed by a provider
            // print(designer_assets_valid.show() + ", " + require_pfp_valid.show() + ", " + address_changed_with_socials.show() + ", " + signed_by_provider.show());
            if (designer_assets_valid == false || require_pfp_valid == false || address_changed_with_socials || signed_by_provider) {

                pz_datum_is_reset: Bool = (
                    new_datum.nft.get("image") == new_datum.extra.get("standard_image")
                    && new_datum.extra.get("image_hash") == new_datum.extra.get("standard_image_hash")
                    && pz_data.get_safe("pfp_asset") == Option[Data]::None
                    && pz_data.get_safe("bg_asset") == Option[Data]::None
                    && pz_data.get_safe("pfp_image") == Option[Data]::None
                    && pz_data.get_safe("bg_image") == Option[Data]::None
                    && pz_data.get_safe("designer") == Option[Data]::None
                    && Int::from_data(pz_data.get("trial")) == 0
                    && Int::from_data(pz_data.get("nsfw")) == 0);

                assert(pz_datum_is_reset, "Personalization properties not properly reset");

                if (address_changed_with_socials) {
                    assert(pz_data.get_safe("socials") == Option[Data]::None, "Socials need to be reset");
                    true
                }
                else {
                    assert(pz_data.get_safe("socials") == prev_datum_extra.get_safe("socials"), "Socials shouldn't be reset");
                    true
                }
            }
            else {
                error("Reset is not allowed or not authorized")
            }
        },
        RETURN_TO_SENDER => {
            settings: PzSettings = loadPzSettings(ctx);
            // This can check for our signature to return anything that isn't a Handle Reference Token
            // It has to have a datum though since cardano-node won't call a smart contract without datum
            ctx.tx.outputs.all((output: TxOutput) -> Bool {output.value.to_map().all((policy:MintingPolicyHash, names: Map[ByteArray]Int) -> Bool {
                    policy != HANDLE_POLICY || names.all((name: ByteArray, _) -> Bool{name.starts_with(LBL_100) == false})
                })
            })
            && adminHasSignedTx(settings, ctx)
        },
        else => {
            false
        }
    }
}